# Maintainer: David Grayson <davidegrayson@gmail.com>

pkgname=$CHOST-psxstub
conflicts=($CHOST-psxscl)
pkgver=r8.11957ad
pkgrel=1
pkgdesc="Stub that stands in for psxscl for building the toolchain."
arch=('any')
url="http://git.midipix.org/cgit.cgi/psxstub/"
license=('unknown')  # psxstub has no license info
makedepends=($CHOST-gcc-stage1)
source=("git://midipix.org/psxstub")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/psxstub"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  rm -rf "${srcdir}/build-$CHOST"
  mkdir "${srcdir}/build-$CHOST"
  cd "${srcdir}/build-$CHOST"
  cp ../psxstub/stub.S ../psxstub/Makefile .
  make TARGET="$CHOST"
}

package() {
  cd "${srcdir}/build-$CHOST"
  make TARGET="$CHOST" DESTDIR="${pkgdir}/usr/$CHOST" install
}
