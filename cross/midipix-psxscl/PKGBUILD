# Maintainer: David Grayson <davidegrayson@gmail.com>

pkgname=$CHOST-psxscl
provides=($CHOST-psxstub)
conflicts=($CHOST-psxstub)
pkgver=r260.f603112
pkgrel=1
pkgdesc="A thread-safe system call layer library"
arch=('any')
url="http://git.midipix.org/$midipix_internal/psxscl"
license=('GPL2' 'GPL3')
depends=()
makedepends=($CHOST-{gcc,ntapi,psxtypes,pemagine,dalist})
source=("git://midipix.org/$midipix_internal/psxscl")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/psxscl"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/psxscl"

  # midipix will probably fix this soon; remove this patch
  {
    echo "#include <psx_utimens.h>"
    cat src/pollen/_select.c
  } > tmp
  mv tmp src/pollen/_select.c
}

build() {
  rm -rf "${srcdir}/build-$CHOST"
  mkdir "${srcdir}/build-$CHOST"
  cd "${srcdir}/build-$CHOST"

  ../psxscl/configure --host="$CHOST" --prefix="" --debug
}

package() {
  cd "${srcdir}/build-$CHOST"
  make DESTDIR="${pkgdir}/usr/$CHOST" install

  cd "${srcdir}/psxscl"
  install -Dm644 COPYING.PSXSCL "${pkgdir}/usr/$CHOST/share/licenses/psxscl/COPYING.PSXSCL"
}
