# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=gmp
pkgname=$CHOST-$_realname
pkgver=6.1.0
pkgrel=1
pkgdesc='A library for arbitrary precision arithmetic.'
arch=('any')
url='http://gmplib.org/'
license=('LGPL3', 'GPL')
makedepends=("${CHOST}-gcc")
source=("https://gmplib.org/download/gmp/gmp-${pkgver}.tar.xz"{,.sig}
        "http://git.midipix.org/cgit.cgi/ports/portage/plain/config.cache"
        midipix.patch)
sha256sums=('68dadacce515b0f8a54f510edf07c1b636492bcdb8e8d54c56eb216225d16989'
            'SKIP'
            '1e8fa44f9da32cf7b33309ce9b60f42427b6c83a7e59aea8f4d577a8a805fc34'
            'bafcadba03736731e7fcbd3a7450b843314398112d43ebff74000ffe15ecc8aa')
validpgpkeys=('5972C48224C1E7E62AA0275D1150EE33C1A000B0')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ../midipix.patch
}

build() {
  rm -rf "build-${CHOST}"
  mkdir "build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"
  cp ../config.cache .
  "../${_realname}-${pkgver}/configure" -C \
    --disable-assembly \
    --host="${CHOST}" --prefix=""
  make
}

check() {
  cd "build-${CHOST}"
  # TODO for native compilation: make check
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}/usr/${CHOST}" install
}
