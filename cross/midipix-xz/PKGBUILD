# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=xz
pkgname=$CHOST-$_realname
pkgver=5.2.2
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('any')
url='http://tukaani.org/xz'
license=('GPL' 'LGPL' 'custom')
makedepends=($CHOST-gcc)
source=("http://tukaani.org/xz/xz-${pkgver}.tar.gz"{,.sig}
        "http://git.midipix.org/cgit.cgi/ports/portage/plain/config.cache"
        midipix-config.patch)
sha256sums=('73df4d5d34f0468bd57d09f2d8af363e95ed6cc3a4a86129d2f2c366259902a2'
            'SKIP'
            'SKIP'
            '0750bd6bad458220945c81dc96b5513239508228e435a1417d6277227ab618fb')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ../midipix-config.patch
}

build() {
  rm -rf "build-$CHOST"
  mkdir "build-$CHOST"
  cd "${srcdir}/build-$CHOST"
  cp ../config.cache .
  "../${_realname}-${pkgver}/configure" -C \
      --host="$CHOST" --prefix=""
  make
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}/usr/${CHOST}" install
}
