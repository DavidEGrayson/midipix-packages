# Maintainer: David Grayson <davidegrayson@gmail.com>

_realname=gettext
pkgname=$CHOST-$_realname
pkgver=0.19.7
pkgrel=1
pkgdesc='Tools for producing multi-lingual messages'
arch=('any')
url='https://www.gnu.org/software/gettext/'
license=('?')
makedepends=($CHOST-gcc)
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-${pkgver}.tar.xz"{,.sig}
        "http://git.midipix.org/cgit.cgi/ports/portage/plain/config.cache"
        midipix-config.patch)
sha256sums=('378fa86a091cec3acdece3c961bb8d8c0689906287809a8daa79dc0c6398d934'
            'SKIP'
            '1e8fa44f9da32cf7b33309ce9b60f42427b6c83a7e59aea8f4d577a8a805fc34'
            'bfaa9408dcded460a20bf842906dfdcb174f304a0e445f8705b2dd3b660c65d8')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ../midipix-config.patch
}

build() {
  rm -rf "build-${CHOST}"
  mkdir "build-${CHOST}"
  cd "${srcdir}/build-${CHOST}"
  cp ../config.cache .
  "../${_realname}-${pkgver}/configure" -C \
    --host="$CHOST" --prefix="" \
    --disable-java --disable-native-java \
    --disable-csharp \
    --enable-shared
  make LIBTOOL=slibtool
}

package() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR="${pkgdir}/usr/${CHOST}" install

  # TODO: copy license
}
