# Maintainer: David Grayson <davidegrayson@gmail.com>

# TODO: probably should use the midipix config.cache
# TODO: install info files using an install script like Arch Linux does?
# TODO: install an inputrc file?

_realname=readline
pkgname=$CHOST-$_realname
_basever=6.3
_patchlevel=008
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc='GNU readline library'
url='https://cnswww.cns.cwru.edu/php/chet/readline/rltop.html'
license=('GPL')
arch=('any')
depends=("${CHOST}-ncurses")
makedepends=("${CHOST}-gcc")
# backup=("/usr/${CHOST}/etc/inputrc")
options=('!emptydirs')
source=("https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz"{,.sig})
sha256sums=('56ba6071b9462f980c5a72ab0023893b65ba6debb4eeb475d7a563dc65cafd43'
            'SKIP'
            '1a79bbb6eaee750e0d6f7f3d059b30a45fc54e8e388a8e05e9c3ae598590146f'
            'SKIP'
            '39e304c7a526888f9e112e733848215736fb7b9d540729b9e31f3347b7a1e0a5'
            'SKIP'
            'ec41bdd8b00fd884e847708513df41d51b1243cecb680189e31b7173d01ca52f'
            'SKIP'
            '4547b906fb2570866c21887807de5dee19838a60a1afb66385b272155e4355cc'
            'SKIP'
            '877788f9228d1a9907a4bcfe3d6dd0439c08d728949458b41208d9bf9060274b'
            'SKIP'
            '5c237ab3c6c97c23cf52b2a118adc265b7fb411b57c93a5f7c221d50fafbe556'
            'SKIP'
            '4d79b5a2adec3c2e8114cbd3d63c1771f7c6cf64035368624903d257014f5bea'
            'SKIP'
            '3bc093cf526ceac23eb80256b0ec87fa1735540d659742107b6284d635c43787'
            'SKIP')
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB')  # Chet Ramey

for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
  source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p){,.sig})
done

prepare() {
  cd "$_realname-$_basever"
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  # patch -p1 -i ../01-include-wchar-h.patch

  # Note: Arch Linux does something with RPATH here
}

build() {
  rm -rf "build-$CHOST"
  mkdir "build-$CHOST"
  cd "build-$CHOST"

  # _GNU_SOURCE (or _XOPEN_SOURCE) is needed for wchar.h to actually define a
  # prototype for wcwidth.
  CFLAGS="$CFLAGS -D_GNU_SOURCE"

  "../$_realname-$_basever/configure" --prefix=/usr/"$CHOST"
  make
}

package() {
  cd "build-$CHOST"
  make DESTDIR="$pkgdir" install
}
