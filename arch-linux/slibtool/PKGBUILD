# Maintainer: David Grayson <davidegrayson@gmail.com>

pkgname=slibtool
pkgver=0.5.5
pkgrel=1
pkgdesc="A skinny libtool implementation, written in C."
arch=('i686' 'x86_64')
url="http://git.midipix.org/cgit.cgi/slibtool"
license=('MIT')
source=("http://midipix.org/dl/slibtool/slibtool-${pkgver}.tar.xz"{,.sig})
sha256sums=('5b43ab54c7d40ec72225e3bacaf03098f77e92d79aa6ef3840b51cfab6be7c8e'
            'SKIP')
validpgpkeys=('6482133FE45A8A91EEB0733716997AE880F70A46')

build() {
  rm -rf "${srcdir}/build-$CARCH"
  mkdir "${srcdir}/build-$CARCH"
  cd "${srcdir}/build-$CARCH"

  "../slibtool-${pkgver}/configure" --prefix=/usr
  make
}

package() {
  cd "${srcdir}/build-$CARCH"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/slibtool-${pkgver}"
  install -Dm644 COPYING.SLIBTOOL "${pkgdir}/usr/share/licenses/slibtool/COPYING.SLIBTOOL"
}
