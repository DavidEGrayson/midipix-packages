# Maintainer: David Grayson <davidegrayson@gmail.com>

pkgname=slibtool
pkgver=0.5.3
pkgrel=1
pkgdesc="Reimplementation of libtool: a generic library support script"
arch=('i686' 'x86_64')
url="http://git.midipix.org/cgit.cgi/slibtool"
license=('MIT')
source=("http://midipix.org/dl/slibtool/slibtool-${pkgver}.tar.xz"{,.sig})
sha256sums=('604334de4c076a3ac800e8a76b277b907412475d1f14539dc4ce156f1fecd336'
            'SKIP')
validpgpkeys=('6482133FE45A8A91EEB0733716997AE880F70A46')

build() {
  rm -rf "${srcdir}/build-$CARCH"
  mkdir "${srcdir}/build-$CARCH"
  cd "${srcdir}/build-$CARCH"

  "../slibtool-${pkgver}/configure" --prefix=/usr
  make
}

package() {
  cd "${srcdir}/build-$CARCH"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/slibtool-${pkgver}"
  install -Dm644 COPYING.SLIBTOOL "${pkgdir}/usr/share/licenses/slibtool/COPYING.SLIBTOOL"
}
