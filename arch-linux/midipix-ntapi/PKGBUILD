# Maintainer: David Grayson <davidegrayson@gmail.com>

_target=x86_64-nt64-midipix

pkgname=$_target-ntapi
pkgver=r40.3d5fe99
pkgrel=1
pkgdesc="Windows NT native API core library"
arch=('any')
url="http://git.midipix.org/$midipix_internal/ntapi"
license=('GPL2' 'GPL3')
makedepends=($_target-gcc)
options=(!strip)
source=("git://midipix.org/$midipix_internal/ntapi")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/ntapi"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  rm -rf "${srcdir}/build-$_target"
  mkdir "${srcdir}/build-$_target"
  cd "${srcdir}/build-$_target"

  # Override Arch Linux compile flags (which are not recognized
  # by our toolchain) with our own.
  export CFLAGS="-g3 -O0"  # easy debugging for now
  export LDFLAGS=""

  ../ntapi/configure --debug --host="$_target"
}

package() {
  cd "${srcdir}/build-$_target"
  make DESTDIR="${pkgdir}/usr/${_target}" install

  cd "${srcdir}/ntapi"
  install -Dm644 COPYING.NTAPI "${pkgdir}/usr/$_target/share/licenses/ntapi/COPYING.NTAPI"
}
