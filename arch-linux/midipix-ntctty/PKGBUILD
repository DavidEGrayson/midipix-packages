# Maintainer: David Grayson <davidegrayson@gmail.com>

_host=x86_64-nt64-midipix

pkgname=$_host-ntctty
pkgver=r53.9ab4322
pkgrel=1
pkgdesc="A thread-safe system call layer library"
arch=('any')
url="http://git.midipix.org/$midipix_internal/ntctty"
license=('MIT')
depends=()
makedepends=($_host-{gcc,psxtypes,dalist,pemagine,ntcon,ntapi})
options=(!strip)
source=("git://midipix.org/$midipix_internal/ntctty")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/ntctty"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  rm -rf "${srcdir}/build-$_host"
  mkdir "${srcdir}/build-$_host"
  cd "${srcdir}/build-$_host"

  # Override Arch Linux compile flags (which are not recognized
  # by our toolchain) with our own.
  export CFLAGS="-g3 -O0"  # easy debugging for now
  export LDFLAGS=""

  ../ntctty/configure --debug --host="$_host"
}

package() {
  cd "${srcdir}/build-$_host"
  make DESTDIR="${pkgdir}/usr/${_host}" install

  cd "${srcdir}/ntctty"
  install -Dm644 COPYING.NTCTTY "${pkgdir}/usr/${_host}/share/licenses/ntctty/COPYING.NTCTTY"
}
