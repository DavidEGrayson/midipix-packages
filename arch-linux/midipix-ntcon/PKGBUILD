# Maintainer: David Grayson <davidegrayson@gmail.com>

_host=x86_64-nt64-midipix

pkgname=$_host-ntcon
pkgver=r7.b895c9b
pkgrel=1
pkgdesc="A thread-safe system call layer library"
arch=('any')
url="http://git.midipix.org/$midipix_internal/ntcon"
license=('MIT')
depends=($_host-pemagine)
makedepends=($_host-gcc $_host-pemagine)
options=(!strip staticlibs)
source=("git://midipix.org/$midipix_internal/ntcon")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/ntcon"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  rm -rf "${srcdir}/build-$_host"
  mkdir "${srcdir}/build-$_host"
  cd "${srcdir}/build-$_host"

  # Override Arch Linux compile flags (which are not recognized
  # by our toolchain) with our own.
  export CFLAGS="-g3 -O0"  # easy debugging for now
  export LDFLAGS=""

  ../ntcon/configure --debug --host="$_host"
}

package() {
  cd "${srcdir}/build-$_host"
  make DESTDIR="${pkgdir}/usr/${_host}" install

  cd "${srcdir}/ntcon"
  install -Dm644 COPYING.NTCON "${pkgdir}/usr/${_host}/share/licenses/ntcon/COPYING.NTCON"
}
