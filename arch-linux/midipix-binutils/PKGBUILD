# Maintainer: David Grayson <davidegrayson@gmail.com>

# Warning: The 32-bit linker scripts like /usr/lib/ldscripts/i386pe.x
# might cause a conflict with a future binutils for midipix32.

_target=x86_64-nt64-midipix

pkgname=$_target-binutils
pkgver=2.24.51
pkgrel=1
pkgdesc="Programs to assemble and manipulate binary and object files for midipix"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('zlib')
source=("ftp://sourceware.org/pub/binutils/snapshots/binutils-${pkgver}.tar.bz2"
        "http://git.midipix.org/cgit.cgi/ports/portage/plain/binutils-${pkgver}.midipix.patch")
sha256sums=('250d3b2925c6b211fb16173b0b25bc091c58829fbcad3eb849645e0af52cf7fa'
            'a0416bd5ae94dcf08fea96b31e1cc129c0731bad17a2e92a5a835f4c72770359')

prepare() {
  cd "${srcdir}/binutils-${pkgver}"
  rm -f bfd/midipix.h  # this file might already be present from a previous run
  patch -p1 -i "../binutils-${pkgver}.midipix.patch"
}

build() {
  rm -rf "${srcdir}/build"
  mkdir "${srcdir}/build"

  cd "${srcdir}/build"

  # If we leave CPPFLAGS alone, the configure script for libiberty in binutils
  # 2.24.51 thinks that limits.h does not exist, because limits.h includes
  # features.h which issues a warning about how optimizations are required to
  # use _FORTIFY_SOURCE, because makepkg.conf defines _FORTIFY_SOURCE=2.  You
  # can reproduce this with:
  #     CPPFLAGS=-D_FORTIFY_SOURCE=2 ../binutils-2.24.51/libiberty/configure
  #     grep HAVE_LIMITS_H config.h
  # Solution: Add -O2 to the CPP flags to silence the warning.
  CPPFLAGS="-O2 $CPPFLAGS"

  ${srcdir}/binutils-${pkgver}/configure \
    --prefix=/usr \
    --with-sysroot="/usr/$_target" \
    --target=${_target} \
    --disable-werror

  # This checks the host environment for necessary tools.
  make configure-host

  make
}

package() {
  cd "${srcdir}/build"

  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install

  for bin in ar as nm objcopy objdump ranlib strip readelf dlltool; do
    rm -f ${pkgdir}/usr/bin/${bin}
  done

  for info in as bfd binutils gprof ld; do
    mv ${pkgdir}/usr/share/info/${info}.info ${pkgdir}/usr/share/info/${_target}-${info}.info
  done

  rm -r ${pkgdir}/usr/share/locale
}
